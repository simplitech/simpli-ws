group 'br.com.simpli'
version '1.2.2'

apply plugin: 'maven'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'signing'

buildscript {
    ext.kotlin_version = '1.3.50'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.9"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api group: 'commons-codec', name: 'commons-codec', version: '1.4'
    api group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.329'
    api group: 'com.amazonaws', name: 'aws-java-sdk-ec2', version: '1.11.329'
    api group: 'com.amazonaws', name: 'aws-java-sdk-ses', version: '1.11.329'
    api group: 'com.amazonaws', name: 'aws-java-sdk-sns', version: '1.11.329'
    api group: 'com.amazonaws', name: 'aws-java-sdk-elasticsearch', version: '1.11.329'
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.28'
    implementation group: 'log4j', name: 'log4j', version: '1.2.17'
    implementation 'br.com.simpli:simpli-tools:1.0.0'
    api 'br.com.simpli:simpli-model:1.1.0'
    implementation 'com.github.fernandospr:javapns-jdk16:2.4.0'
    implementation 'com.google.code.gson:gson:2.2.4'
    api 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.1.0'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: ossUser, password: ossPassword)
            }

            pom.project {
                name 'Simpli WS'
                description 'A collection of tools from Simpli used for web services'
                url 'https://github.com/simplitech/simpli-ws'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'joao.lippi'
                        name 'Joao Pedro Lippi'
                        email 'joao.lippi@simpli.com.br'
                    }
                    developer {
                        id 'melanke'
                        name 'Gil Lopes Bueno'
                        email 'gil.bueno@simpli.com.br'
                    }
                }
                scm {
                    connection 'scm:git:git://github.com/simplitech/simpli-ws.git'
                    developerConnection 'scm:git:ssh://github.com/simplitech/simpli-ws.git'
                    url 'https://github.com/simplitech/simpli-ws'
                }
            }
        }
    }
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}